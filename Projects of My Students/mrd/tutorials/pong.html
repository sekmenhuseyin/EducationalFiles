<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1" />
<title>pong</title>
</head>
<body bgcolor="#cccccc">
<!--url's used in the movie-->
<!--text used in the movie-->
<object classid="clsid:d27cdb6e-ae6d-11cf-96b8-444553540000" codebase="http://download.macromedia.com/pub/shockwave/cabs/flash/swflash.cab#version=7,0,0,0" width="550" height="400" id="pong" align="middle">
<param name="allowScriptAccess" value="sameDomain" />
<param name="movie" value="pong.swf" />
<param name="quality" value="high" />
<param name="bgcolor" value="#000000" />
<embed src="pong.swf" quality="high" bgcolor="#000000" width="550" height="400" name="pong" align="middle" allowScriptAccess="sameDomain" type="application/x-shockwave-flash" pluginspage="http://www.macromedia.com/go/getflashplayer" />
</object>

<h3>code in <i>begin</i> frame </h3>
<pre>
//Pong
//By Andy Harris
//For Flash Game Programming for Dummies
//March '05
//Most code in "play" frame

stop();

btnPlay.onRelease = function(){
  _root.gotoAndStop("play");
} // end button
</pre>

<h3>code in <i>play</i> frame </h3>
<pre>
//pong
//working game with win and lose statess

init();

function init(){
  //turn off the mouse pointer
  Mouse.hide();

  //initialize ball speed
  ball.dx = 15;
  ball.dy = 5;

  //opp speed is opponent speed
  //determines max dy for opponent.
  //15 is nearly flawless
  //0 is dead
  opp.speed = 14;

  //reset scores
  playerScore = 0;
  oppScore = 0;
  
} // end init

player.onEnterFrame = function(){
  player._y = _root._ymouse;
} // end enterFrame

opp.onEnterFrame = function(){
  //opp._y = ball._y;
  //check to see if ball is above or below opp
  if (ball._y < opp._y){
    //move opp up
    opp.dy = - opp.speed;
  } else {
    //move opp down
    opp.dy = opp.speed;
  } // end if

  opp._y += opp.dy;
  
} // end function

ball.onEnterFrame = function(){
  ball.move();
  ball.checkBoundaries();
  ball.checkPaddles();
} // end enterFrame

ball.move = function(){
  ball._x += ball.dx;
  ball._y += ball.dy;
} // end move

ball.checkBoundaries = function(){
  //bounce off top and bottom walls
  if (ball._y < 0){
    ball.dy = -ball.dy;
  } // end if

  if (ball._y > Stage.height){
    ball.dy = -ball.dy;
  } // end if

  //if past left of screen, opponent scores 
  if (ball._x < 0){
    trace("Opponent Scores");
    oppScore++;
    //check for opponent win
    if (oppScore >= 3){
      gotoAndStop("lose");
    } // endif
    //opponent serves
    ball._x = opp._x - 50;
    ball._y = opp_y;
    ball.dy = 0;

  } // end if

  //if past right of stage, player scores, 
  if (ball._x > Stage.width){
    trace ("Player scores");
    playerScore++;
    if (playerScore >= 3){
      gotoAndStop("win");
    } // end if
    //player serves
    ball._x = player._x + 50;
    ball._y = player._y;
    ball.dy =0;    
  } // end if
} // end checkboundaries

ball.checkPaddles = function(){
  //check to see if ball touches paddle
  if (ball.hitTest(player)){
    //simply bounce off for now
    ball.dx = -ball.dx;
    ball.dy = getDy(player);
  } // end if

  if (ball.hitTest(opp)){
    ball.dx = -ball.dx;
    ball.dy = getDy(opp);
  } // end if
} // end checkPaddles
  
function getDy(paddle){
  //determines dy based on where ball hits paddle.
  
  //relY is relative Y of ball to paddle
  relY = ball._y - paddle._y;
  //trace relY

  //relPerc is relY / height of paddle
  //will range from -.5 to +.5
  relPerc = relY / paddle._height;
  //trace (relPerc);

  //new DY ranges from -15 to +15
  newDy = relPerc * 30;
  //trace (newDy);
  return newDy;

} // end getDy
</pre>

<h3>code in <i>win</i> frame </h3>
<pre>
Mouse.show();

btnPlay.onRelease = function(){
  _root.gotoAndStop("play");
} // end button
</pre>

<h3>code in <i>lose</i> frame </h3>
<pre>
Mouse.show();

btnPlay.onRelease = function(){
  _root.gotoAndStop("play");
} // end button
</pre>


</body>
</html>
